/*****************************************************************************
 * Copyright (c) 2015 CEA LIST and others.
 * 
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License 2.0
 * which accompanies this distribution, and is available at
 * https://www.eclipse.org/legal/epl-2.0/
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *   Nicolas FAUVERGUE (ALL4TEC) nicolas.fauvergue@all4tec.net - Initial API and implementation
 *   
 *****************************************************************************/
grammar org.eclipse.papyrus.uml.textedit.valuespecification.xtext.UmlValueSpecification with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/uml2/5.0.0/UML" as uml
import "http://www.eclipse.org/emf/2002/Ecore" as ecore


generate umlValueSpecification "http://www.eclipse.org/papyrus/uml/textedit/valuespecification/xtext/UmlValueSpecification"
	
AbstractRule:
	(visibility=VisibilityKind)?
	(name=VALUE_SPECIFICATION_ID)?
	((instanceSpecification=[uml::InstanceSpecification]) |
	value=(LiteralBooleanRule |
		   LiteralIntegerOrUnlimitedNaturalRule |
		   LiteralRealRule |
		   LiteralNullRule |
		   LiteralStringRule) |
	undefined=UndefinedRule)
;

LiteralBooleanRule:
	value=('true'|'false')
;

LiteralIntegerOrUnlimitedNaturalRule:
	(value=(INT|VALUE_SPECIFICATION_NEGATIVE_INT) |
	unlimited='*')
;

LiteralRealRule:
	value=VALUE_SPECIFICATION_DOUBLE
;


LiteralNullRule:
	value='null'
;

LiteralStringRule:
	value=STRING
;

UndefinedRule:
	value='<Undefined>'
;

VisibilityKind:
	public = '+'
	| private = '-'
	| protected = '#'
	| package = '~' ;
	
// Manage the basic terminals
terminal VALUE_SPECIFICATION_ID returns ecore::EString: ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')*'=';
terminal VALUE_SPECIFICATION_NEGATIVE_INT returns ecore::EInt: '-'INT;
terminal VALUE_SPECIFICATION_DOUBLE returns ecore::EDouble: ('-')?((INT('.'|',')INT)|(INT('.'|','))|(('.'|',')INT));
